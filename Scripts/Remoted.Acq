***************************************************
* Live Acquisition Script (2.0)                   *
* - Pulls Forensics artifacts for analysis        *
* - Created by D0n Quix0te/OMENScan (May 2015)    *
* - Inspired by many other tools                  *
***************************************************
SAY:
SAY: Now Running Remoted AChoir Script.
SAY:
SAY: * * *   Please MAKE SURE you are running this as an Administrator  * * * 
SAY: * * * Administrator right are required for many of these utilities * * * 
SAY:
PZZ: Press Any Key to Continue, or q to QUIT
SAY:
SAY: 1. Now Dumping Memory...
SAY:
SAY:  WARNING: This will take while, and will create a HUGE memory image File.
SAY:  You've been warned ;-) 
SAY:
SAY: Checking for available Disk Space (Min Required: &MEM)
SAY:
N>>:&Dsa &Mem
 ACQ:\MemDump
 EXE:\MEM\WinPmem.exe &Acq\WinPmemDump.Raw
END:
N<<:&Dsa &Mem
 SAY: Not Enough disk space in &ACQ to Capture Memory: &MEM...  Bypassing Memory Capture!
END:
SAY:
SAY: 2. Extracting Prefetch Directory...
SAY:
ACQ:\Prf
NCP:"&Win\prefetch\*" "&Acq"
SAY:
SAY: 3a. Copy Raw $MFT...
SAY:
ACQ:\RawData
NCP:"C:\$MFT" "&Acq"
SAY:
SAY: 3b. Copy Raw $LogFile...
SAY:
NCP:"C:\$LogFile" "&Acq"
SAY:
SAY: 3c. Now Parsing USNJrnl...
SAY:
CMD:fsutil usn enumdata 1 0 1 C: > &Acq\USNJrnl.dat
CMD:fsutil usn readjournal C: csv >> &Acq\USNJrnl.dat
SAY:
SAY: 3d. Now Extracting USNJrnl...
SAY:
EXE:&Map\DSK\ExtractUSNJrnl.exe /DevicePath:c: /OutputPath:&Acq
SAY:
SAY: 4a. Gathering System Information...
SAY:
ACQ:\
CMD:&Map\SYS\PSInfo.exe /accepteula -s > &Acq\Info.dat
SAY:
SAY: 4b. Gathering System Audit Information...
SAY:
EXE:&Map\SYS\WinAudit.exe /r=gsoPxuTUeERNtnzDaIbMpmidcSArCOHG /f=&Acq\WinAudit.htm /l=&Acq\WinAudLog.txt
SAY:
SAY: 4c. Gathering Group Policy Information...
SAY:
CMD:GPResult /R /Z > &Acq\GPResult.txt
SAY:
SAY: 5. Raw Copy Registry Hives...
SAY:
ACQ:\Reg
NCP:"&Win\System32\Config\SECURITY" "&Acq"
NCP:"&Win\System32\Config\SOFTWARE" "&Acq"
NCP:"&Win\System32\Config\SAM" "&Acq"
NCP:"&Win\System32\Config\SYSTEM" "&Acq"
NCP:"&Win\AppCompat\Programs\Amcache.hve" "&Acq"
SAY:
SAY: 6. Copying (System32) EventViewer Logs...
SAY:
ACQ:\Evt
ACQ:\Evt\Sys32
NCP:"&Win\System32\winevt\Logs\*" "&Acq"
SAY:
SAY: 6a. Copying (Sysnative) EventViewer Logs...
SAY:
ACQ:\Evt\Nativ
NCP:"&Win\sysnative\winevt\Logs\*" "&Acq"
SAY:
SAY: 7. Parsing (System32) Etc Directory, and SRUM...
SAY:
ACQ:\SYS
ACQ:\SYS\Sys32
NCP:"&Win\System32\Drivers\Etc\*" "&Acq"
NCP:"&Win\System32\sru\SRUDB.dat" "&Acq"
SAY:
SAY: 7a. Parsing (Sysnative) Etc Directory, and SRUM...
SAY:
ACQ:\SYS\Nativ
NCP:"&Win\sysnative\Drivers\Etc\*" "&Acq"
NCP:"&Win\sysnative\sru\SRUDB.dat" "&Acq"
SAY:
SAY: 8. Parsing Environment Variables...
SAY:
ACQ:\SYS
CMD:Set > &Acq\EnVar.dat
SAY:
SAY: 9. Parsing The Patch List...
SAY:
CMD:WMIC qfe list > &Acq\QFEList.dat
SAY:
SAY: 10. Gathering Running Process List Information...
SAY:
CMD:Tasklist /v > &Acq\Tasklist.dat
CMD:Tasklist /M > &Acq\TaskAll.dat
CMD:&Map\SYS\PSList.exe /accepteula -x > &Acq\PSList.dat
SAY:
SAY: 11. Gathering Connection Information...
SAY:
***:Replaced NETSTAT with CPORTS (Gives Full Path of Process)
***:NetStat -abno > &Acq\NetStat.dat
EXE:\SYS\cports.exe /scomma &Acq\CPorts.csv
SAY:
SAY: 12. Gathering IP Config Information...
SAY:
CMD:IPConfig /all > &Acq\IPConfig.dat
CMD:IPConfig /DisplayDNS > &Acq\IPCfgDNS.dat
SAY:
SAY: 13. Gathering ARP Information...
SAY:
CMD:Arp -a > &Acq\ArpInfo.dat
SAY:
SAY: 14. Gathering NetBIOS Information...
SAY:
CKY:&Win\System32\NBTStat.exe
CMD:&Win\System32\NBTStat.exe -scn > &Acq\NetBios.dat
END:
CKY:&Win\sysnative\NBTStat.exe
CMD:&Win\sysnative\NBTStat.exe -scn >> &Acq\NetBios.dat
END:
SAY:
SAY: 15. Gathering Open Files/Process Information...
SAY:
CMD:&Map\SYS\Handle.exe /accepteula > &Acq\OpenFiles.dat
SAY:
SAY: 16. Gathering Scheduled Task Information...
SAY:
CMD:At > &Acq\SchedTasks.dat
CMD:Schtasks /query /fo LIST /v >> &Acq\SchedTasks.dat
SAY:
SAY: 17. Gathering System Logon Information...
SAY:
CMD:&Map\SYS\PSLoggedon.exe /accepteula > &Acq\Logon.dat
SAY:
SAY: 18. Gathering Services Information...
SAY:
CMD:Net Start > &Acq\Services.dat
CMD:sc query type= service state= all > &Acq\Services-2.dat
CMD:Tasklist /SVC > &Acq\Services-3.dat
SAY:
SAY: 19. Gathering Last Activity Information...
SAY:
EXE:&Map\SYS\LastActivityView.exe /scomma &Acq\LastActivity.csv
SAY:
SAY: 20. Gathering User Assist Information...
SAY:
EXE:&Map\SYS\UserAssistView.exe /scomma &Acq\UserAssist.csv
SAY:
SAY: 21. Gathering AutoRun Information...
SAY:
ACQ:\Arn
ARN:
SAY:
CMD:&Map\SYS\Autorunsc.exe /accepteula -a * -c -h > &Acq\AutoRun.dat
SAY:
SAY: 22. Gathering All User Registries...
SAY:
ACQ:\Reg
CMD:Reg Save HKCU &Acq\NTUSER.DAT
SAY:
NCP:"C:\Users\*\NTUSER.DAT" "&Acq"
SAY:
SAY: 23. Now Extracting Browser History...
SAY:
ACQ:\Brw
EXE:&Map\SYS\BrowsingHistoryView.exe /scomma &Acq\BrowseHist.csv
EXE:&Map\SYS\BrowsingHistoryView.exe /shtml &Acq\BrowseHist.htm
SAY:
SAY: 24. Gathering $Recycle.Bin entries...
SAY:
DSK:Fixed
ACQ:\RBin
NCP:"&Dsk:\$Recycle.Bin\*" "&Acq"
SAY:
SAY: Acquisition Complete...
SAY:
SAY: Now Hashing Artifacts...
HSH:ACQ
SAY:
PZZ: All Done!  Press Any Key to Finish.
